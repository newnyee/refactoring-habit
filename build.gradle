plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'jacoco'
}

group = 'com'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'

    // Mockito
    testImplementation 'org.mockito:mockito-core:3.12.4'

    // querydsl
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // mapstruct
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    testAnnotationProcessor "org.mapstruct:mapstruct-processor:1.5.3.Final"

    // spring-security-core
    implementation 'org.springframework.security:spring-security-core'

    // Bouncy Castle Provider
    implementation 'org.bouncycastle:bcprov-jdk18on:1.76'

    // Mail
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    // Thymeleaf Layout Dialect(for email service)
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'

    // Auth0 JWT
    implementation 'com.auth0:java-jwt:4.4.0'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

jacoco {
    toolVersion = '0.8.10'
}

jacocoTestReport {
    reports {
        html.required = true
        xml.required = false
        csv.required = false
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect{
            fileTree(dir: it, include: [
                    '**/service/**',
                    '**/interceptor/api/**',
                    '**/interceptor/view/**'
            ])
        }))
    }

    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
    violationRules {
        // 프로젝트 커버리지 목표
        rule {
            element = 'CLASS'
            includes = [
                    '*.service.*',
                    '*.interceptor.api.*',
                    '*.interceptor.view.*'
            ]
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.80  // 클래스 단위의 결정 커버리지 기준 (80%)
            }
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80  // 클래스 단위의 구문 커버리지 기준 (80%)
            }
        }
    }
}

test {
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
}
